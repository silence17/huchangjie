plugins {
    id 'com.android.library'
    id 'kotlin-android'
    //apply plugin: ‘maven’ 不再支持
    //发布maven配置
    id 'maven-publish'
}

android {
    compileSdk 31

    defaultConfig {
        minSdk 21
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

//打包main目录下代码和资源的 task
task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
}

// 用于把aar打包到maven仓库的任务
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifact androidSourcesJar

                groupId = 'com.hucj.gradle'
                artifactId = 'testlib'
                version = '1.0.0'
            }

            // Creates a Maven publication called “debug”.
            debug(MavenPublication) {
                //没有这个不能在maven仓库中生成aar/jar文件
                from components.debug
                //指定要发布的文件 aar/jar
                artifact androidSourcesJar

                groupId = 'com.hucj.gradle'
                artifactId = 'testlib'
                version = '1.0.0-SNAPSHOT'
            }
        }

        //仓库地址
        repositories {
            maven {
                // 仓库默认不允许使用非https协议，所以这里设置为允许
                allowInsecureProtocol true
                //本地的Maven地址
                url 'file:///Users/huchangjie/.m2/repository'
            }
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}